allprojects {
    group 'org.artfable'
    version '1.0-RELEASE'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: "kotlin-spring"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        kotlin_version = '1.2.30'
        spring_boot_version = '1.5.9.RELEASE'
        spring_dependency_version = '1.0.4.RELEASE'
        h2_version = '1.4.196'
        liquibase_version = '3.5.3'

        junit_version = '4.11'
        mockito_version = '2.13.0'
    }

    dependencyManagement {
        dependencies {
            dependency "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            dependencySet(group: 'org.springframework.boot', version: spring_boot_version) {
                entry 'spring-boot-starter-web'
                entry 'spring-boot-starter-security'
                entry 'spring-boot-starter-data-jpa'
            }
            dependency "com.h2database:h2:$h2_version"
            dependency "org.liquibase:liquibase-core:$liquibase_version"

            dependency group: 'junit', name: 'junit', version: junit_version
            dependency "org.mockito:mockito-core:$mockito_version"
        }
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

buildscript {
    ext {
        kotlin_version = '1.2.30'
        spring_boot_version = '1.5.9.RELEASE'
        spring_dependency_version = '1.0.4.RELEASE'
        sass_version = '0.0.1'
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "io.spring.gradle:dependency-management-plugin:$spring_dependency_version"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
        classpath "com.github.artfable.gradle:gradle-sass-plugin:$sass_version"
    }

}

